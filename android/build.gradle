plugins {
  id 'com.android.library' version '8.2.0' apply false
}

apply plugin: 'com.android.library'

def androidSdkVersion = 34
def javaVersion = 17

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(javaVersion)
  }
}

dependencies {
  api "io.github.lambdaprime:id.xfunction:25.0:android@aar"
  api "io.github.lambdaprime:jrosmessages:7.0"
}

android {
  namespace "id.jros1messages"
  compileSdk androidSdkVersion
  compileOptions {
    sourceCompatibility javaVersion
  }
  lintOptions {
    abortOnError false
  }
  sourceSets {
    main {
      java {
        srcDirs += ["../jros1messages/src/main/gen/java"]
      }
    }
  }
  defaultConfig {
    targetSdkVersion androidSdkVersion
    minSdkVersion androidSdkVersion
  }
}

// Android build complains on "requires java.logging;" inside module-info.java
// for no obvious reason because JUL is part of Android API (see https://developer.android.com/reference/java/util/logging/package-summary)
// It is not possible to exclude module-info through srcDirs:
// https://discuss.gradle.org/t/how-to-exclude-module-info-java-in-gradle-7/42816
// instead we copy sources except module-info to separate folder
task copySources(type: Copy) {
  from "../jros1messages/src/main/java"
  into "src/main/java"
  exclude "**/module-info.java"
}
preBuild.dependsOn copySources